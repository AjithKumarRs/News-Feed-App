apply plugin: 'com.android.application'
apply plugin: 'jacoco'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'

apply from: '../jacoco.gradle'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "com.voltek.newsfeed"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 2
        versionName "1.1"
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    buildTypes {

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
            testCoverageEnabled true
        }

        buildTypes.each {
            it.buildConfigField 'String', 'ApiKey', ApiKey
        }
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Instrumentation tests
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$versions.espresso", {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$versions.espresso", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
    }
    androidTestImplementation "org.mockito:mockito-android:$versions.mockito"

    // Unit tests
    testImplementation "junit:junit:$versions.junit"
    testImplementation "org.mockito:mockito-core:$versions.mockito"
    testImplementation "com.squareup.okhttp3:mockwebserver:$versions.mockwebserver", {
        exclude group: 'com.squareup.okio', module: 'okio'
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:$versions.mockitoKotlin", {
        exclude group: 'org.jetbrains.kotlin'
    }

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$versions.kotlinGradlePlugin"

    // Android support library
    implementation "com.android.support:appcompat-v7:$versions.supportLibrary"
    implementation "com.android.support:design:$versions.supportLibrary"
    implementation "com.android.support:recyclerview-v7:$versions.supportLibrary"

    // Networking
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofit"

    // Rx
    implementation "io.reactivex.rxjava2:rxjava:$versions.rxJava"
    implementation "io.reactivex.rxjava2:rxandroid:$versions.rxAndroid"
    implementation "io.reactivex.rxjava2:rxkotlin:$versions.rxKotlin"
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$versions.rxBindingKotlin"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:$versions.rxBindingKotlin"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:$versions.rxBindingKotlin"

    // Dependency injection
    implementation "com.google.dagger:dagger:$versions.dagger"
    kapt "com.google.dagger:dagger-compiler:$versions.dagger"

    // Images
    implementation "com.github.bumptech.glide:glide:$versions.glide"

    // Data storage
    implementation "com.orhanobut:hawk:$versions.hawk"
    implementation "com.github.vicpinm:krealmextensions:$versions.kotlinRealmExtensions"

    // Parcel
    implementation "org.parceler:parceler-api:$versions.parceler"
    kapt "org.parceler:parceler:$versions.parceler"

    // Logging
    implementation "com.jakewharton.timber:timber:$versions.timber"

    // Fonts
    implementation "uk.co.chrisjenx:calligraphy:$versions.calligraphy"

    // MVP
    implementation "com.arello-mobile:moxy:$versions.moxy"
    implementation "com.arello-mobile:moxy-app-compat:$versions.moxy"
    kapt "com.arello-mobile:moxy-compiler:$versions.moxy"

    // Animations
    implementation "jp.wasabeef:recyclerview-animators:$versions.recyclerviewAnimators"
}
